grammar com.github.funthomas424242.language.Rezept with org.eclipse.xtext.common.Terminals

generate rezept "http://www.github.com/funthomas424242/language/Rezept"
 

 
Rezept:
	projectBeschreibung = ProjektBeschreibung | rezeptListe = Rezeptliste | produktListe= Produktliste;
 

ProjektBeschreibung:     
 'RezeptSammlung' '{'
 		'groupId' '=' groupId=STRING ';'
 		'artifactId' '=' artifactId=STRING ';'
 		'version' '=' version=STRING ';'
 		'Media Folder' ':' resourcesDir=STRING ';'
	 	('siteURL' '=' siteURL=STRING ';')?
 '}' buch=BuchBeschreibung 
 ;
 
 BuchBeschreibung:
 	'Buch' '{'
 	(imports+=RezeptImport)* 
 	'Titel:' titel=STRING ';'
 	(autoren+=Autor)+
 	('Vorwort:' vorwort=STRING 	(absatz+=Absatz)* )?
 	(lizenz=Lizenz)?
 	('Veröffentlicht am:' datumPublished=DATUM '.')?
 	'}'
 ;
 
 Absatz:
 	'Absatz:' 
 	(titel=HEADER)?
 	text=STRING
 ;
 
 Autor:
 	'Autor:' '{'
 	'Vorname:' vorname=STRING
 	'Nachname:' nachname=STRING
 	('Organisation:' organisationsName=STRING)?
 	'ModifikationsNotiz:' modifikationsNotiz=STRING
 	'}'
 ;
 
 Lizenz:
 	'Lizenzhinweis:' hinweis=STRING
 	('Lizenztext:' text=STRING)?
 ;
 	
 RezeptImport:
 	'import rezepte' importURI = STRING ';'	
 ;
 
 
 Produktliste:
 	'Produktliste' name=STRING ';' 
 	//(imports
 	(rezepte+=ProduktBlatt)+
 ;
 
 ProduktBlatt:
 	'Produkt' name=STRING '{'
	 ('Verpackung:' verpackung=Verpackung '.')?
	 menge=BestimmteMenge
	 ('EAN:' ean=STRING '.')?
	 ('UBA:' uba=STRING '.')?
	 preis=Preis?
	 ('Händler:' haendler=STRING '.')?
	 ('Hersteller:' hersteller=STRING '.')?
	 'Letzte Änderung:' datumLetzteAenderung=DATUM '.'
 '}';
 
 Preis:
 	'Preis:' betrag=STRING waehrung=STRING '.'
 ;
 
 Rezeptliste:
 	'Rezeptliste' name=STRING ';' 
 	(imports+=ProduktImport)*
 	(rezepte+=RezeptBlatt)+
 ;
 
 ProduktImport:
 	'import produkte' importedResource=STRING ';'
 ;
 
 RezeptBlatt:
 	'Rezept' id=STRING '{'
	 	'Titel:' titel=STRING '.'
	 	('Untertitel:' untertitel=STRING '.')?
	 	'Kategorie:' kategorie=STRING '.'
	 	('Quelle:' quelle=Quelle)?
	 	('Ausreichend für' personen=INT 'Personen.')? 	
	 	'Letzte Änderung:' letzteAenderung=DATUM '.'
	 	(tags+=Tag)+
	 	(produkte+=ProduktRef)*
	 	(zutaten+=Zutat)+
	 	(schritte+=Arbeitsschritt)+
	 	'--Zusatzinformationen--'
	 	(notizen+=Notiz)*
	 	(bilder+=Bild)*
 '}';
 
 Arbeitsschritt:
 	'Aktion:' beschreibung=STRING 
 	(notiz=Notiz)?
 ;
 
 Notiz:
 	Tipp | Kommentar | Anmerkung
 ;
 
 Tipp:
 	'Tipp:' text=STRING
 ;
 
 Kommentar:
 	'Kommentar:' text=STRING
 ;
 
 Anmerkung:
 	'Anmerkung:' text=STRING
 ;
 
 Bild:
 	'Bild:' ablageort=Ablageort
 	('Text:' beschreibung=STRING)?
 	('Lizenz:' lizenz=STRING)
 	('Urheber:' urheber=STRING)?
 	'.'
 ;
 
 Ablageort:
 	Internetpfad | Lokalerpfad
 ;
 
 Internetpfad:
 	'URL:' url=STRING
 ;
 
 Lokalerpfad:
 	'FileName:' fileName=STRING
 ;
 
 ProduktRef:
 	'ProduktRef:' produkt=STRING
 	'Menge:' anzahl=STRING
 	'mal.'
 ;
 
 Zutat:
 	'Zutat:' name=STRING menge=Menge 
 ;
 
 Menge:
 	BestimmteMenge | UnbestimmteMenge
 ;
 
 BestimmteMenge:
 	'Menge:' betrag=STRING details=STRING? einheit=Masseinheit '.'
 ;
 
 UnbestimmteMenge:
 	menge=UnbestimmteMengenangabe '.'
 ;

 Quelle:
 	Literaturquelle | Personenquelle
 ;
 
 Literaturquelle:
 	'übernommen' modifikation=Modifikationsart 'aus' beschreibung=STRING '.'
 ;
 
 
 Personenquelle:
 	'überliefert' 'von' personenBeschreibung=STRING '.'
 ;
 
 Tag:
 	DiaetTag | StoffTag | BenutzerTag
 ;
 
 StoffTag:
 	'Stoff' stoff=STRING '.'
 ;
 
 BenutzerTag:
 	'Tag:' bezeichnung=STRING '.'
 ;
 
 DiaetTag:
 	'Diät:' diaet=STRING '.'
 ;
 
 Name:
 	BEZEICHNER
 ;
 
QualifiedName:
   BEZEICHNER ('.' BEZEICHNER)*
 ;
 
 enum Modifikationsart:
 	MODIFIZIERT='"verändert"' | UNVERAENDERT='"unverändert"'
 ;
 
 enum Masseinheit:
 	  KG='"kg"'
 	| G='"g"' 
 	| ML='"ml"'
 	| L='"L"'
 	| STUECK='"Stück"'
 	| EL='"Esslöffel"'
 	| TL='"Teelöffel"'
 	| TUETEN='"Tüte(n)"'
 	| PCKCH='"Päckchen"'
 	| SCHEIBEN='"Scheibe(n)"'
 	| DOSE='"Dose(n)"'
 ;
 
 enum UnbestimmteMengenangabe:
 	  NACH_BEDARF='"nach Bedarf"'
 	| ETWAS='"etwas"'
 	| WENIG='"wenig"'
 ;
 
 enum Verpackung:
 	  GLAS='"Glas"'
 	| DOSE='"Dose"'
 	| TUETE='"Tüte"'
 	| NETZ='"Netz"'
 	| PCKG='"Packung"'
 	| PCKCH='"Päckchen"'
 	| BECHER='"Becher"'
 ;
 
 terminal BEZEICHNER : ('a'..'z'|'A'..'Z'|'ä'..'ü'| 'Ä'..'Ü'|'#'|'_') ('a'..'z'|'A'..'Z'|'ä'..'ü'| 'Ä'..'Ü'|'#'|'_'|'0'..'9')*;
 terminal DATUM : ('0'..'9') ('0'..'9') '.' ('0'..'9') ('0'..'9') '.' ('0'..'9') ('0'..'9')('0'..'9') ('0'..'9');
 terminal HEADER :
			'==' ( '\\' . /* 'b'|'t'|'n'|'f'|'r'|'u'|'"'|"'"|'\\' */ | !('\\'|'=')  )+ '==' |
			'--' ( '\\' . /* 'b'|'t'|'n'|'f'|'r'|'u'|'"'|"'"|'\\' */ | !('\\'|'-')  )+ '--'
			;
 


  
  
 